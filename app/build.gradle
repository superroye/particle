apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

apt {
    arguments {
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
        resourcePackageName android.defaultConfig.applicationId
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.substance.particle"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 131
        versionName "1.3.1"
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "channel_name"]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        maxProcessCount 4 // this is the default value
        javaMaxHeapSize "2g"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            jni.srcDirs = []//disable automatic ndk-build call
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("ojia.keystore")
            storePassword "****"
            keyAlias "***"
            keyPassword "***"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            //不显示Log
            buildConfigField "boolean", "LOG_INFO", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ''
        }

    }

    productFlavors {
        hiyd {}
        qqmyapp {}
        qqmyapp1 {}
        baidu {}
        baidu1 {}
        baidu2 {}
        baidu3 {}
        baidutieba {}
        m91 {}
        ant {}
        m360 {}
        xiaomi {}
        dwhz {}
        dwbbs {}
        fanhe{}
        fat_train{}
        fensitong{}
        fensitong1{}
        vivo{}
        huawei{}
        yingyongbao{}
        feeds{}
        yuansheng{}
    }

    // 批量配置渠道
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace(".apk", "-${defaultConfig.versionName}.apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
            }
    }

}

def androidSupportVersion = '24.+'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    apt "org.androidannotations:androidannotations:3.+"
    compile 'org.androidannotations:androidannotations-api:3.+'
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile 'com.github.kidd3166:ABase:6fe7947fc9'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.4.1'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0@aar'
    compile 'com.sprylab.android.texturevideoview:texturevideoview:1.1.1'
    compile 'com.mikhaellopez:circularprogressbar:1.1.1'
    compile 'com.github.magiepooh:recycler-itemdecoration:1.1.1@aar'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    compile 'tv.danmaku.ijk.media:ijkplayer-java:0.6.0'
    compile 'tv.danmaku.ijk.media:ijkplayer-armv7a:0.6.0'
}
